#version 430

#extension GL_ARB_shading_language_include : require
#include </data/shaders/ism/ism_utils.glsl>
#include </data/shaders/common/reprojection.glsl>

struct VPL {
    vec4 position;
    vec4 normal;
    vec4 color;
};

layout (local_size_x = 8, local_size_y = 8, local_size_z = 1) in;
layout (r11f_g11f_b10f, binding = 0) restrict writeonly uniform image2D img_output;
layout (rg16ui, binding = 1) restrict readonly uniform uimage3D lightListIdsAndSizes;
layout (r16ui, binding = 2) restrict readonly uniform uimage2D lightLists;

const int totalVplCount = 1024;
layout (std140, binding = 0) uniform vplBuffer_
{
    VPL vplBuffer[totalVplCount];
};

uniform sampler2D faceNormalSampler;
uniform sampler2D depthSampler;
uniform sampler2D ismDepthSampler;

uniform mat4 projectionMatrix;
uniform mat4 projectionInverseMatrix;
uniform mat4 viewMatrix;
uniform mat4 viewInvertedMatrix;
uniform mat4 viewProjectionInvertedMatrix;
uniform float zFar;
uniform float zNear;
uniform ivec2 viewport;

uniform float giIntensityFactor;
uniform float vplClampingValue;

// don't forget that these uniforms eat performance
uniform int vplStartIndex = 0;
uniform int vplEndIndex = totalVplCount;
int vplCount = vplEndIndex - vplStartIndex;
uniform bool scaleISMs = false;
float ismIndexOffset = scaleISMs ? vplStartIndex : 0;
int ismCount = (scaleISMs) ? vplCount : totalVplCount;
int ismIndices1d = int(pow(2, ceil(log2(ismCount) / 2))); // next even power of two
uniform bool enableShadowing = true;

uniform bool showVPLPositions = false;

uniform bool useInterleaving = true;
uint interleavedSize = useInterleaving ? 4 : 1;
uint interleavedPixels = interleavedSize*interleavedSize;
// number of bits that will be taken from gl_WorkGroupID to determine the interleavedPixel
uint interleaveBits = uint(log2(interleavedSize));
// set interleaveBits rightmost bits to 1
uint interleavedPixelBitmask = (1u << interleaveBits) - 1u;

const uint clusterPixelSize = 128;


void main()
{
    uvec2 largeInterleaveBlockPosition = (gl_WorkGroupID.xy >> interleaveBits) * gl_WorkGroupSize.xy * interleavedSize;
    uvec2 offsetInLargeInterleaveBlock = gl_LocalInvocationID.xy * interleavedSize;
    uvec2 interleavedPixel = gl_WorkGroupID.xy & interleavedPixelBitmask;
    ivec2 fragCoord = ivec2(largeInterleaveBlockPosition + offsetInLargeInterleaveBlock + interleavedPixel);

    vec2 v_uv = vec2(fragCoord) / viewport;

    // TODO maybe view rays again? could re-use view z for cluster coord
    float depthSample = texelFetch(depthSampler, fragCoord, 0).r;
    vec4 ndc = vec4(v_uv, depthSample, 1.0) * 2.0 - 1.0;
    vec4 fragWorldCoordWithW = viewProjectionInvertedMatrix * ndc;
    vec3 fragWorldCoord = fragWorldCoordWithW.xyz / fragWorldCoordWithW.w;


    vec3 fragNormal = texelFetch(faceNormalSampler, fragCoord, 0).xyz * 2.0 - 1.0;

    const int numDepthSlices = 16;
    const int numSlicesIntoFirstSlice = 3;
    float scaleFactor = (numDepthSlices + numSlicesIntoFirstSlice) / log2(50);
    float depth = linearDepth(depthSample, projectionMatrix);
    int clusterZ = int(max(log2(-depth) * scaleFactor - numSlicesIntoFirstSlice, 0));


    uvec2 clusterCoord = uvec2(fragCoord.xy) / clusterPixelSize;
    uvec2 lightListIdAndSize = imageLoad(lightListIdsAndSizes, ivec3(clusterCoord, clusterZ)).rg;
    uint lightListId = lightListIdAndSize.r;
    uint numVPLs = lightListIdAndSize.g;

    // this is never true, but for some reason, it speeds this up by about 20% or 2.5ms
    if (numVPLs < min(imageLoad(lightLists, ivec2(lightListId, 0)).r, 0u))
        numVPLs = 0;

    uint interleavedPixel1d = interleavedPixel.x + interleavedPixel.y * interleavedSize;
    int startIndex = int(float(numVPLs) / interleavedPixels * interleavedPixel1d);
    int endIndex = int(float(numVPLs) / interleavedPixels * (interleavedPixel1d + 1));

    vec3 acc = vec3(0.0);

    for (int lightIndex = startIndex; lightIndex < endIndex; lightIndex++) {
        uint vplIndex = imageLoad(lightLists, ivec2(lightListId, lightIndex)).r;
        VPL vpl = vplBuffer[vplIndex];

        vec3 diff = fragWorldCoord - vpl.position.xyz ;
        float dist = length(diff);
        vec3 normalizedDiff = diff / dist;

        // debug splotch
        float isNearLight = 1.0 - step(0.15, dist);
        vec3 debugSplotch = isNearLight * vpl.color.xyz / dist / dist * 0.0001;
        acc += debugSplotch * float(showVPLPositions);

        // geometry term
        float angleFactor = max(0.0, dot(vpl.normal.xyz, normalizedDiff)) * max(0.0, dot(fragNormal, -normalizedDiff));
        float attenuation = 1.0 / pow(dist, 4.0);
        float geometryTerm = angleFactor * attenuation;
        geometryTerm = min(geometryTerm, vplClampingValue);

        // ISM shadowing
        float ismIndex = vplIndex - ismIndexOffset;
        vec3 v = paraboloid_project(diff, dist, vpl.normal.xyz, zFar, ismIndex, ismIndices1d, false);
        float occluderDepth = textureLod(ismDepthSampler, v.xy, 0).x;
        float shadowValue = v.z - occluderDepth;
        shadowValue = smoothstep(0.95, 1.0, 1 - shadowValue);
        shadowValue = mix(1.0, shadowValue, float(enableShadowing));

        acc += vpl.color.xyz * geometryTerm * shadowValue;
    }

    vec3 resultColor = vec3(acc * giIntensityFactor / vplCount) * interleavedPixels;

    imageStore(img_output, fragCoord, vec4(resultColor, 0.0));
}
