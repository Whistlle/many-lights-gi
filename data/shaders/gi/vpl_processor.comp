#version 430

#extension GL_ARB_shading_language_include : require
#include </data/shaders/common/floatpacking.glsl>

uniform sampler2D rsmDiffuseSampler;
uniform sampler2D rsmNormalSampler;
uniform sampler2D rsmDepthSampler;

uniform mat4 biasedLightViewProjectionInverseMatrix;
uniform float lightIntensity;
uniform bool shuffleLights;

const int totalVplCount = 1024;
layout (local_size_x = 64) in;

struct VPL {
    vec3 position;
    vec3 normal;
    vec3 color;
};

layout (packed, binding = 0) buffer vplBuffer_
{
	VPL vplBuffer[];
};

layout (std140, binding = 1) buffer packedVplBuffer_
{
    vec4 packedVplBuffer[];
};

layout (packed, binding = 1) uniform shuffledIndicesBuffer_
{
    int shuffledIndicesBuffer[totalVplCount];
};

const uint rsmSamples1d = uint(ceil(sqrt(totalVplCount)));


void main()
{
    uint resultIndex = gl_WorkGroupID.x * gl_WorkGroupSize.x + gl_LocalInvocationID.x;
    uint totalWorkCount = gl_NumWorkGroups.x * gl_WorkGroupSize.x;

    uvec2 lightsOnAxis = uvec2(rsmSamples1d, totalWorkCount / rsmSamples1d);

    ivec2 samplerSize = textureSize(rsmDiffuseSampler, 0);

    uvec2 texCoords = uvec2(resultIndex % lightsOnAxis.x, resultIndex / lightsOnAxis.x) * samplerSize / lightsOnAxis;
    vec2 texCoordsf = vec2(texCoords) / samplerSize;

    vec3 diffuse = texelFetch(rsmDiffuseSampler, ivec2(texCoords), 0).rgb;
    vec3 normal = texelFetch(rsmNormalSampler, ivec2(texCoords), 0).rgb * 2.0 - 1.0;
    float depth = texelFetch(rsmDepthSampler, ivec2(texCoords), 0).r;

    vec4 worldcoords = biasedLightViewProjectionInverseMatrix * vec4(texCoordsf, depth, 1.0);
    worldcoords.xyz /= worldcoords.w;

    vec3 lightColor = diffuse * lightIntensity;

    if (shuffleLights)
        resultIndex = shuffledIndicesBuffer[resultIndex];

    float packedNormal = pack3SNToFloat(vec3(normal));
    // make sure everyone's accessing the same values
    // gi.comp could also read the packedNormal directly and unpack it, but that's quite slow for some reason
    vec3 unpackedNormal = unpack3SNFromFloat(packedNormal);

    vplBuffer[resultIndex] = VPL(worldcoords.xyz, unpackedNormal, lightColor);
    packedVplBuffer[resultIndex] = vec4(worldcoords.xyz, packedNormal);
}
