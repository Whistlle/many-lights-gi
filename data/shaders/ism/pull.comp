#version 430

#extension GL_ARB_shading_language_include : require
#include </data/shaders/common/floatpacking.glsl>

#define LEVEL_ZERO

layout (local_size_x = 8, local_size_y = 8) in;

layout (binding = 0) uniform usampler3D softrenderBuffer;
layout (rgba32f, binding = 0) restrict readonly uniform image2D ismDepthImage;
layout (rgba32f, binding = 1) restrict writeonly uniform image2D img_output;

uniform int level;
uniform float zFar;
uniform int ismPixelSize;

const float infinity = 1. / 0.;

void main()
{
    ivec2 pixelCoordinate = ivec2(gl_WorkGroupID.xy * gl_WorkGroupSize.xy + gl_LocalInvocationID.xy);

    ivec2[4] offsets = { {0,0}, {0,1}, {1,0}, {1,1} };

    float[4] depthSamples;
    float[4] maxDepths;
    float[4] radiuses;
    vec3[4] normals;
    vec2[4] displacementVectors;
    bool[4] valid;

    for (int i = 0; i < 4; i++) {
        # ifdef LEVEL_ZERO
            float depthSample = float(texelFetch(softrenderBuffer, ivec3(pixelCoordinate * 2 + offsets[i], 0), 0).r) / 500000.0;
            uint normalRadius = texelFetch(softrenderBuffer, ivec3(pixelCoordinate * 2 + offsets[i], 1), 0).r;
            vec2 displacementVector = vec2(0.0);
        # else
            vec4 read = imageLoad(ismDepthImage, pixelCoordinate * 2 + offsets[i]);
            float depthSample = read.r;
            float maxDepth = read.g;
            uint normalRadius = floatBitsToUint(read.b);
            vec2 displacementVector = Unpack2FromFP32(read.a);
        # endif

        vec4 normalRadiusUnpacked = Unpack4PNFromUint(normalRadius);
        vec3 normal = normalRadiusUnpacked.xyz * 2.0 - 1.0;
        float radius = normalRadiusUnpacked.w * 15;

        #ifdef LEVEL_ZERO
            float maxDepth = depthSample + (radius * 2) / zFar;
            // radius is in world units so far, project & convert to pixels
            float distToCamera = depthSample * zFar;
            radius = radius / distToCamera / 3.14 * ismPixelSize; // approximation that breaks especially for near points.
            radius *= 1.3; // boost radius a bit to make circle area match the point rendering square area
            radius = min(radius, 15);
        #endif


        // radius check
        vec2 newDisplacementVector = (offsets[i] - 0.5 + displacementVector) / 2;
        float dist = length(newDisplacementVector);

        float scaledRadius = radius * pow(2, -(level+1));

        bool radiusCheckPassed = dist <= scaledRadius;

        depthSamples[i] = depthSample;
        maxDepths[i] = maxDepth;
        radiuses[i] = radius;
        normals[i] = normal;
        displacementVectors[i] = newDisplacementVector; // TODO blocky results, but are round when displacment vector set to 0
        valid[i] = depthSample != 1.0;
        valid[i] = valid[i] && radiusCheckPassed; // TODO unknown whether this helps
    }

    float minimum = infinity;
    float maxDepth;
    for (int i = 0; i < 4; i++) {
        if (!valid[i])
            continue;
        minimum = min(depthSamples[i], minimum);
        if (minimum == depthSamples[i])
            maxDepth = maxDepths[i];
    }

    for(int i = 0; i < 4; i++) {
        if (depthSamples[i] > maxDepth)
            valid[i] = false;
    }

    float depthAcc = 0.0;
    vec3 normalAcc = vec3(0.0);
    float radiusAcc = 0.0;
    vec2 displacementAcc = vec2(0.0);
    uint numValid = 0;
    float maxValidMaxDepth = 0.0;
    for (int i = 0; i < 4; i++) {
        if (!valid[i])
            continue;

        depthAcc += depthSamples[i];
        displacementAcc += displacementVectors[i];
        radiusAcc += radiuses[i];
        normalAcc += normals[i];
        maxValidMaxDepth = max(maxValidMaxDepth, maxDepths[i]);
        numValid++;
    }

    vec4 result;
    if (numValid > 0) {
        result.r = depthAcc / numValid;
        result.g = maxValidMaxDepth;
        result.b = Pack4PNToFP32(vec4(normalize(normalAcc) * 0.5 + 0.5, radiusAcc / numValid / 15.0));
        result.a = Pack2ForFP32(displacementAcc / numValid);
    } else {
        result = vec4(1.0, 0.0, 0.0, 0.0);
    }

    imageStore(img_output, pixelCoordinate, result);
}
